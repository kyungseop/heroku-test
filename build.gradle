plugins {
    id 'org.springframework.boot' version '2.7.3' apply false
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'java'
    id "org.asciidoctor.jvm.convert" version "3.3.2" apply false
    id "com.epages.restdocs-api-spec" version "0.16.2" apply false
    id 'jacoco'
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

def javaProjects = [
        project(":common"),
        project(":core"),
        project(":clients:feign-common"),
        project(":admin"),
        project(":api"),
]

configure(javaProjects) {
    apply plugin: "java"
    apply plugin: "jacoco"
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    group = "${projectGroup}"
    version = "${projectVersion}-${new Date().format('yyyyMMddHHmmss')}"

    sourceCompatibility = 11
    targetCompatibility = 11

    dependencyManagement {
        imports {
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:2021.0.3")
            mavenBom("io.awspring.cloud:spring-cloud-aws-dependencies:2.4.2")
            mavenBom("com.amazonaws:aws-java-sdk-bom:1.12.262")
            mavenBom("org.testcontainers:testcontainers-bom:1.17.3")
        }
    }

    dependencies {
        compileOnly("com.google.code.findbugs:jsr305:3.0.2")
        compileOnly("org.projectlombok:lombok")
        annotationProcessor('org.projectlombok:lombok')

        implementation("org.springframework.boot:spring-boot-starter-logging")

        testImplementation("org.springframework.boot:spring-boot-starter-test")
        testImplementation(testFixtures(project(":core")))
    }

    jacoco {
        toolVersion = "0.8.6"
    }

    test {
        reports {
            junitXml.enabled = true
            html.enabled = true
        }
        jacoco {
            enabled = true
            destinationFile = file("${buildDir}/jacoco/jacoco.exec")
        }
        finalizedBy jacocoTestReport

        useJUnitPlatform { //jUnit5 수행
            excludeTags("integrationTest")
        }
    }

    tasks.register("integrationTest", Test) {
        useJUnitPlatform {
            includeTags("integration")
        }
        finalizedBy "jacocoTestReport"
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.setDestination(file("${buildDir}/reports/jacoco/html"))
        }
        getExecutionData().setFrom(files("${buildDir}/jacoco/jacoco.exec"))
    }
}

def querydslProjects = [
        project(":admin"),
        project(":api"),
]

configure(querydslProjects) {
    dependencies {
        implementation("org.springframework.boot:spring-boot-starter-data-jpa")
        implementation("com.querydsl:querydsl-core")
        implementation("com.querydsl:querydsl-jpa")

        annotationProcessor("com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa")
        annotationProcessor("jakarta.persistence:jakarta.persistence-api")
        annotationProcessor("jakarta.annotation:jakarta.annotation-api")
    }
}


def requiredRestDocsProject = [
        project("api")
]

configure(requiredRestDocsProject) {
    apply plugin: "org.asciidoctor.jvm.convert"
    apply plugin: "com.epages.restdocs-api-spec"

    configurations {
        asciidoctorEx
    }

    ext {
        set("snippetsDir", file("build/generated-snippets"))
    }

    dependencies {
        ////testImplementation("org.springframework.restdocs:spring-restdocs-mockmvc")
        testImplementation("com.epages:restdocs-api-spec:0.16.2")
        testImplementation("com.epages:restdocs-api-spec-mockmvc:0.16.2")

        asciidoctorEx("org.springframework.restdocs:spring-restdocs-asciidoctor")
    }

    tasks.register("docsTest", Test) {
        outputs.dir.snippetsDir
        useJUnitPlatform {
            includeTags("docsTest")
        }
        finalizedBy "jacocoTestReport"
        finalizedBy "asciidoctor"
        finalizedBy "openapi3"
    }

    tasks.named("asciidoctor") {
        inputs.dir.snippetsDir
        configurations "asciidoctorEx"
        baseDirFollowsSourceDir()
    }

    openapi3 {
        servers = [
                {
                    url = "http://localhost:8080"
                }
        ]
        title = "api openapi spec"
        //version = "${projectVersion}"
        format = "yaml"
    }

    bootJar {
//        from("${asciidoctor.outputDir}") {
//            into "BOOT-INF/classes/static/docs"
//        }
        from("swagger-ui") {
            into "BOOT-INF/classes/static/swagger-ui"
        }
        from("${project.projectDir}/build/api-spec/openapi3.yaml") {
            into "BOOT-INF/classes/static/swagger-ui"
        }
    }

}
